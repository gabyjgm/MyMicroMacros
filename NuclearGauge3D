// ImageJ macro to measure nuclei using 3D OC - v210311b
// written by Gaby G Martins done for Carolina Pereira CHR lab @IGC2021 
// instead of a robust watershed method, I opted for manual splitting of touching nuclei using alt+paintbrush
// measures volume and surface area of cell nuclei from microscopy images, preferably confocal or deconvolved widefield.
// saves a *txt file with the measurements
// https://github.com/gabyjgm/MyMicroMacros
// alternatively one can try the 3D ROI manager https://imagejdocu.tudor.lu/plugin/stacks/3d_roi_manager/start

dir = getDirectory("image");
title = getTitle();
run("Duplicate...", "title=img duplicate");
selectWindow(title);
resetMinAndMax();
slices = nSlices();
//clears 1st and last slices to avoid exclusion when touching Z edges
setBackgroundColor(0, 0, 0);
setSlice(1); run("Select All"); run("Clear", "slice");
setSlice(slices); run("Next Slice [>]"); run("Select All"); run("Clear", "slice");

// allows user to see touching nuclei and manually split them using alt+paintbrush (must manually select paintbrush)
run("Z Project...", "projection=[Max Intensity]");
rename("tempMIP");
setAutoThreshold("Li dark");
run("Convert to Mask");
run("Fill Holes");
run("Paintbrush Tool Options...", "brush=5");
setTool("Paintbrush Tool");
waitForUser("Using the 'Paintbrush Tool' alt+draw to split nuclei, then [OK]"); 
run("Create Selection");
selectWindow(title);
run("Restore Selection");
setBackgroundColor(0, 0, 0);
run("Clear Outside", "stack");
close("tempMIP");

//proceeed to segment nuclei
run("Select None");
setAutoThreshold("Li dark stack");
run("Convert to Mask", "method=Li background=Dark black");
setOption("BlackBackground", true);
run("Median...", "radius=3 stack");
run("Fill Holes", "stack");

// sets and runs 3D object counter
run("3D OC Options", "volume surface dots_size=5 font_size=20 store_results_within_a_table_named_after_the_image_(macro_friendly) redirect_to=none");
run("3D Objects Counter", "threshold=50 min.=5000 max.=135790592 exclude_objects_on_edges objects statistics");
// creates a preview to confirm segmentation and 3D OC is OK
// user then closes windows 
selectWindow("img");
run("Z Project...", "projection=[Max Intensity]");
run("Enhance Contrast", "saturated=0.35");
run("Apply LUT");
run("8-bit");
selectWindow("Objects map of " + title);
run("Z Project...", "projection=[Max Intensity]");
rename("objsMIP");
run("Enhance Contrast", "saturated=0.35");
run("Apply LUT");
run("Divide...", "value=3");
run("Merge Channels...", "c1=objsMIP c4=MAX_img create");
close("img");
close("MAX_img");
close("objsMIP");

// here macro asks user to check merged image and results, then saves and closes windows
waitForUser("Check that results are good & [OK], otherwise 'Esc' to avoid saving results and closing windows");
close(title);
close("Composite");
close("Objects map of " + title); 
selectWindow("Statistics for " + title);
saveAs("Results", dir + title +" - statistics.txt");
close(title + " - statistics.txt");
open();
